#
# Executes commands at login pre-zshrc.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#

#
# Browser
#

if [[ "$OSTYPE" == darwin* ]]; then
  export BROWSER='open'
fi

#
# Editors
#

export EDITOR='vim'
export VISUAL='vim'
export PAGER='less'

#
# Language
#

if [[ -z "$LANG" ]]; then
  export LANG='es_ES.UTF-8'
fi

#
# Paths
#

# Ensure path arrays do not contain duplicates.
typeset -gU cdpath fpath mailpath path

# Set the list of directories that cd searches.
# cdpath=(
#   $cdpath
# )

# Set the list of directories that Zsh searches for programs.
path=(
  "$HOME/bin"
  /usr/local/{bin,sbin}
  "$HOME/.local/bin"
  "$HOME/.cabal/bin"
  "${GHCUP_INSTALL_BASE_PREFIX:=$HOME}/.ghcup/bin"
  "$HOME/.composer/vendor/bin"
  "$HOME/.cargo/bin"
  $path
)

# this removes whatever is not a directory or doesn't exist
# it lets me modify path in different OSes with less worries
path=($^path(N))

#
# Less
#

# Set the default Less options.
# Mouse-wheel scrolling has been disabled by -X (disable screen clearing).
# Remove -X and -F (exit if the content fits on one screen) to enable it.
export LESS='-i -M -R -w -z-8'

# Set the Less input preprocessor.

# Try source-highlight. It can call lesspipe if compressed files
if (( $#commands[(i)src-hilite-lesspipe(|.sh)] )); then
  # source-highlight will try to fallback to lesspipe for compressed files archives
  # but it might call it as "lesspipe" instead of "lesspipe.sh"
  if (( $#commands[(i)lesspipe.sh] )) && ! (( $#commands[(i)lesspipe] )); then
    alias 'lesspipe=lesspipe.sh'
  fi

  export LESSOPEN="| /usr/bin/env $commands[(i)src-hilite-lesspipe(|.sh)] %s 2>&-" LESS_ADVANCED_PREPROCESSOR=1

elif [ -r /usr/share/source-highlight/src-hilite-lesspipe.sh ]; then
  # source-highlight will try to fallback to lesspipe for compressed files archives
  # but it might call it as "lesspipe" instead of "lesspipe.sh"
  if (( $#commands[(i)lesspipe.sh] )) && ! (( $#commands[(i)lesspipe] )); then
    alias 'lesspipe=lesspipe.sh'
  fi

  export LESSOPEN="|  /usr/share/source-highlight/src-hilite-lesspipe.sh %s 2>&-" LESS_ADVANCED_PREPROCESSOR=1

# Try both `lesspipe` and `lesspipe.sh`. It can call pygmentize if
# properly configured and appropiate
elif (( $#commands[(i)lesspipe(|.sh)] )); then
  export LESSOPEN="| /usr/bin/env $commands[(i)lesspipe(|.sh)] %s 2>&-" LESS_ADVANCED_PREPROCESSOR=1

# Try pygmentize
elif (( $#commands[pygmentize] )); then
  export LESSOPEN="| $commands[pygmentize] %s 2>&-"
fi
